import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.4'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.google.cloud.tools.jib' version "${jibPluginVersion}"
}

group = 'com.pareto'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    implementation 'org.springframework.boot:spring-boot-starter-jetty'

    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
//    implementation 'org.springframework.boot:spring-boot-starter-data-elasticsearch'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${openapiUIVersion}"

    implementation "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    // Minio Object Storage
    implementation "io.minio:minio:${minioVersion}"

    // Mapstruct
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${mapStructBindingVersion}"
    implementation "org.projectlombok:lombok-mapstruct-binding:${mapStructBindingVersion}"

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation "redis.clients:jedis:${jedisClientVersion}"

    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.aspectj:aspectjrt:1.9.7'
    implementation 'org.aspectj:aspectjweaver:1.9.7'
}

def timestamp = LocalDateTime.now().format(DateTimeFormatter.ofPattern("yyyy-MM-dd_HH-mm-ss"))

jib {
    from {
        image = "gcr.io/distroless/java21"
    }
    to {
        image = "docker.io/mr3iscuit/${rootProject.name}:latest"
        tags = ['latest', '0.0.1', timestamp, "gh-run-" + System.getenv("GITHUB_RUN_NUMBER") ?: "notset"]
        auth {
            username = System.getenv("DOCKER_USERNAME") ?: "your-dockerhub-username"
            password = System.getenv("DOCKER_PASSWORD") ?: "your-dockerhub-password"
        }
    }
    container {
        ports = ["8080"]
        jvmFlags = ["-Xms512m", "-Xmx1024m"]
        mainClass = "com.pareto.activities.ActivitiesApplication"
        format = "OCI"
    }
    container.creationTime = "USE_CURRENT_TIMESTAMP"
    configurationName = "runtimeClasspath"
}

tasks.named('test') {
    useJUnitPlatform()
}
